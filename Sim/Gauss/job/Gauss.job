#!/bin/csh
###########################################################
#   Script for running Gauss in interactive and batch
###########################################################
#
# in BATCH, output files are written in the BATCH machine
# and copied at the end to your $MYWORKDIR output directory
# in INTERACTIVE output files are written to the current directory
#
set MODE = $CMTCONFIG

if ($#argv == 0 ) then
  echo "\nUsage: Gauss.job <version> [<mode>]"
  echo " where:"
  echo "  <version> (mandatory) is the Gauss version number (e.g. v19r1)"
  echo "  <mode>    (optional)  is execution mode (e.g. $CMTDEB)"
  exit 0
else
  set Gversion = $1
  if ($#argv == 2) then
    set MODE = "$2" 
    shift
  endif
  shift
endif
#
echo "----> Executing Gauss $Gversion using as options Gauss.opts (mode $MODE)"
#

if ($?LS_SUBCWD) then 
# in BATCH, WORKDIR is set by the system
# in BATCH, location of executable has to be given explicitly - e.g.:
  setenv MYJOBROOT $HOME/cmtuser/Sim/Gauss/$Gversion
#  setenv MYJOBROOT $LHCBDEV/GAUSS/GAUSS_v19r4/Sim/Gauss/$Gversion
else
# in interactive set WORKDIR to submission directory
  set WORKDIR = $PWD
# in interactive, executable is under same root directory as this command file
  cd `dirname $0` 
  cd ..
  set MYJOBROOT = $PWD
endif

# == set the program environment
if ($?Gauss_release_area) then
  source $Gauss_release_area/GaussEnv/$Gversion/cmt/setup.csh
else
  source $LHCBRELEASES/GaussEnv/$Gversion/cmt/setup.csh
endif
echo "----> MYJOBROOT is set to:"
echo $MYJOBROOT
echo "----> CMTPATH is set to:"
echo $CMTPATH
source $MYJOBROOT/cmt/setup.csh -tag=$MODE

# Go to working directory
cd $WORKDIR
echo "----> Working in $PWD"

if ($?LS_SUBCWD) then
# == in batch copy the executable and options files to $WORKDIR
  cp $MYJOBROOT/$MODE/Gauss.exe tmpjob.exe
  cp $MYJOBROOT/options/*.opts .
  setenv GAUSSOPTS .
else
# == in interactive create a link to the executable
  ln -fs $MYJOBROOT/$MODE/Gauss.exe tmpjob.exe
endif

# == run the excutable ================================
if ($MODE == $CMTDEB) then
  setenv JOBOPTPATH $GAUSSOPTS/Gauss.opts
  gvd tmpjob.exe
else
  time tmpjob.exe $GAUSSOPTS/Gauss.opts
endif

#  Is there a core dump
set corefile = `ls -a | grep core`
if ($corefile != "") then
   if(-e gdbcmds) rm gdbcmds
   echo where > gdbcmds
   echo quit >> gdbcmds
   gdb -x gdbcmds tmpjob.exe $corefile<<EOF
EOF
rm gdbcmds
endif

# == job is finished: remove temporary files ==========
rm -f tmpjob.exe
if ($?LS_SUBCWD) then
# if in batch copy Gauss.sim file to permanent storage (example for CERN)
#  rfcp Gauss.sim $CASTOR_HOME/Gauss/$Gversion/30000000.500ev.3.sim
#  rm -f Gauss.sim
# if in batch copy output files to user directories
  ls *.* >! RETURN
endif

exit 0

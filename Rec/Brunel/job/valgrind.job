#!/bin/csh
###########################################################
#   Initialize LSF queue parameters
#
# "-q" gives the queue (1nh by default)
# "-c" gives the CPU time in minutes
#
# -q 8nm  = about 30 min HP735     express
# -q 1nh           3 hours         short
# -q 8nh           1 day           medium
# -q 1nd           3 days          long
# -q 1nw          10 days HP K260  verylong
#
#
#  define the job name
#BSUB -J brunel
#  define the queue
#BSUB -q 8nh
#
###########################################################
#
# in BATCH, output files are written in the BATCH machine
# and copied at the end to your $MYWORKDIR output directory
# in INTERACTIVE output files are written to the current directory
#

if ($#argv == 0 ) then
  echo "\nUsage: valgrind.job <options>"
  echo " where:"
  echo "  <options> (mandatory) is name of options file (e.g. v200601)"
  exit 0
else
  set Dver = $1
  shift
endif
#   
set Bversion = "v31r9"
echo "Executing valgrind for Brunel $Bversion using options file $Dver.opts"
#

if ($?LS_SUBCWD) then 
# in BATCH, WORKDIR is set by the system
# in BATCH, location of executable has to be given explicitly
  setenv MYJOBROOT ~/cmtuser/Brunel_$Bversion/Rec/Brunel/$Bversion
else
# in interactive set WORKDIR to submission directory
  set WORKDIR = $PWD
# in interactive, executable is under same root directory as this command file
  cd `dirname $0` 
  cd ..
  set MYJOBROOT = $PWD
endif

# == set the program environment
source /afs/cern.ch/lhcb/group/rich/vol4/jonrob/scripts/new-valgrind.csh

source $MYJOBROOT/cmt/setup.csh -tag=$CMTDEB

# Go to working directory
cd $WORKDIR

if ($?LS_SUBCWD) then
# == in batch copy the executable and options files to $WORKDIR
  cp $MYJOBROOT/$CMTDEB/Brunel.exe tmpjob_$Dver.exe
  cp $MYJOBROOT/options/*.opts .
  cp $MYJOBROOT/job/Brunel.supp Brunel.supp
  setenv BRUNELOPTS .
else
# == in interactive create a link to the executable
  ln -fs $MYJOBROOT/$CMTDEB/Brunel.exe tmpjob_$Dver.exe
  cp $BRUNELOPTS/$Dver.opts .
endif
cp $Dver.opts valgrind.opts
echo "ApplicationMgr.EvtMax = 5;" >> valgrind.opts

# == run the excutable ================================
set SuppFile = "./Brunel.supp"

valgrind --tool=memcheck --demangle=no --error-limit=no --leak-check=yes --num-callers=40 --suppressions=$STDOPTS/Gaudi.supp --suppressions=$SuppFile ./tmpjob_$Dver.exe valgrind.opts >& memcheck.log

# == job is finished: remove temporary files ==========
rm tmpjob_$Dver.exe
rm valgrind.opts
if ($?LS_SUBCWD) then
  rm -f *.opts
# if in batch copy output files to user directories
  ls *.* >! RETURN
endif

exit 0

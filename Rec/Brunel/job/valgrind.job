#!/bin/csh
###########################################################
#   Initialize LSF queue parameters
#
# "-q" gives the queue (1nh by default)
# "-c" gives the CPU time in minutes
#
# -q 8nm  = about 30 min HP735     express
# -q 1nh           3 hours         short
# -q 8nh           1 day           medium
# -q 1nd           3 days          long
# -q 1nw          10 days HP K260  verylong
#
#
#  define the job name
#BSUB -J brunel
#  define the queue
#BSUB -q 8nh
#
###########################################################
#
# in BATCH, output files are written in the BATCH machine
# and copied at the end to your $MYWORKDIR output directory
# in INTERACTIVE output files are written to the current directory
#

if ($#argv == 0 ) then
  echo "\nUsage: valgrind.job <dbase-version>"
  echo " where:"
  echo "  <dbase-version> (mandatory) is SICB/dbase version (e.g. v241)"
  exit 0
else
  echo "using SICB/dbase version $1"
  setenv LHCBDBASE $LHCBSOFT/SICB/dbase/$1
  set Dver = $1
  shift
endif
#   
set Bversion = "v20r0"
#

if ($?LS_SUBCWD) then 
# in BATCH, WORKDIR is set by the system
# in BATCH, location of executable has to be given explicitly
  setenv MYJOBROOT ~/newmycmt/Rec/Brunel/$Bversion
else
# in interactive set WORKDIR to submission directory
  set WORKDIR = $PWD
# in interactive, executable is under same root directory as this command file
  cd `dirname $0` 
  cd ..
  set MYJOBROOT = $PWD
endif

# == set the program environment
if ($?Brunel_release_area) then
  source $Brunel_release_area/BrunelEnv/$Bversion/cmt/setup.csh
else
  source $LHCb_release_area/BrunelEnv/$Bversion/cmt/setup.csh
endif
source $MYJOBROOT/cmt/setup.csh -tag=$CMTDEB

# Go to working directory
cd $WORKDIR

if ($?LS_SUBCWD) then
# == in batch copy the executable and options files to $WORKDIR
  cp $MYJOBROOT/$CMTDEB/Brunel.exe tmpjob_$Dver.exe
  cp $MYJOBROOT/options/*.opts .
  cp $MYJOBROOT/options/Brunel.cards tmpjob.cards
  cp $MYJOBROOT/job/Brunel.supp Brunel.supp
  setenv BRUNELOPTS .
  setenv SICBCARDS tmpjob.cards
else
# == in interactive create a link to the executable
  ln -fs $MYJOBROOT/$CMTDEB/Brunel.exe tmpjob_$Dver.exe
endif

# == run the excutable ================================
if( $CMTCONFIG == "rh73_gcc32" ) then
  set SuppFile = "./Brunel.supp.gcc32"
else
  set SuppFile = "./Brunel.supp"
endif

valgrind --demangle=no --error-limit=no --leak-check=yes --num-callers=20 --suppressions=$SuppFile tmpjob_$Dver.exe >& mem_errors.log

# == job is finished: remove temporary files ==========
rm tmpjob_$Dver.exe
if ($?LS_SUBCWD) then
  rm -f tmpjob.cards
  rm -f *.opts
# if in batch copy output files to user directories
  ls *.* >! RETURN
endif

exit 0

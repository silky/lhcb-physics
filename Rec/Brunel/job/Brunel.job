#!/bin/csh
###########################################################
#   Initialize LSF queue parameters
#
# "-q" gives the queue (1nh by default)
# "-c" gives the CPU time in minutes
#
# -q 8nm  = about 30 min HP735     express
# -q 1nh           3 hours         short
# -q 8nh           1 day           medium
# -q 1nd           3 days          long
# -q 1nw          10 days HP K260  verylong
#
#
#  define the job name
#BSUB -J brunel
#  define the queue
#BSUB -q 8nh
#
###########################################################
#
# in BATCH, output files are written in the BATCH machine
# and copied at the end to your $MYWORKDIR output directory
# in INTERACTIVE output files are written to the current directory
#
if ($#argv == 0 ) then
  echo "\nUsage: Brunel.job <dbase-version> [<mode>]"
  echo " where:"
  echo "  <dbase-version> (mandatory) is SICB/dbase version (e.g. v241)"
  echo "  <mode>          (optional)  is execution mode (e.g. LinuxStatic)"
  exit 0
else
  echo "using SICB/dbase version $1"
  setenv LHCBDBASE $LHCBSOFT/SICB/dbase/$1
endif
#   

set MODE = $CMTCONFIG
if ($?LS_SUBCWD) then 
# in BATCH, WORKDIR is set by the system
# in BATCH, location of executable has to be given explicitly
  setenv MYJOBROOT ~/newmycmt/Rec/Brunel/v14r1p2
else
# in interactive set WORKDIR to submission directory
  set WORKDIR = $PWD
  if ($#argv == 2) set MODE = "$2" 
# in interactive, executable is under same root directory as this command file
  cd `dirname $0` 
  cd ..
  set MYJOBROOT = $PWD
endif

# == set the program environment
source $MYJOBROOT/cmt/setup.csh -tag=$MODE

# Go to working directory
cd $WORKDIR

if ($?LS_SUBCWD) then
# == in batch copy the executable and options files to $WORKDIR
  cp $MYJOBROOT/$MODE/Brunel.exe tmpjob_$1.exe
  cp $MYJOBROOT/options/*.opts .
  cp $MYJOBROOT/options/Brunel.cards tmpjob.cards
  setenv BRUNELOPTS .
  setenv SICBCARDS tmpjob.cards
else
# == in interactive create a link to the executable
  ln -fs $MYJOBROOT/$MODE/Brunel.exe tmpjob_$1.exe
endif

# == run the excutable ================================
if ($MODE == "Linuxdbx") then
  ddd tmpjob_$1.exe $BRUNELOPTS/Brunel$1.opts
else
  time tmpjob_$1.exe $BRUNELOPTS/Brunel$1.opts
endif

#  Is there a core dump
if (-e core) then
   gdb tmpjob_$1.exe core << EOF
where
quit
EOF
endif


# == job is finished: remove temporary files ==========
rm tmpjob_$1.exe
if ($?LS_SUBCWD) then
  rm -f tmpjob.cards
  rm -f *.opts
# if in batch copy output files to user directories
  ls *.* >! RETURN
endif

exit 0








